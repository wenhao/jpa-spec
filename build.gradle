plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "org.owasp.dependencycheck" version "5.2.1"
    id 'checkstyle'
    id 'java-library'
    id 'idea'
    id 'net.ltgt.apt' version '0.21'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "com.github.ksoichiro.spelling" version "0.1.1"
    id "com.simonharrer.modernizer" version "1.6.0-1"
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'
    id 'com.github.ksoichiro.build.info' version '0.2.0'
    id "com.github.ben-manes.versions" version '0.22.0'
    id "net.rdrei.android.buildtimetracker" version "0.11.0"
    id "com.github.hierynomus.license" version "0.15.0"
    id "org.sonarqube" version "2.7.1"
}

repositories {
    jcenter()
}

group = 'com.github.wenhao'
version = '4.0.0-SNAPSHOT'

idea {
    project {
        vcs = 'Git'
    }
    module {
        jdkName = '1.8'
    }
}

dependencies {
    compileOnly "org.springframework.data:spring-data-jpa:$springDataJpaVersion"
    compileOnly "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:$jpaVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.apache.commons:commons-lang3:$commonsLang3Version"
    testImplementation "com.h2database:h2:$h2Version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}

test {
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    useJUnitPlatform()
}

checkstyle {
    toolVersion '8.1'
    ignoreFailures = false
    configFile file("$project.rootDir/config/checkstyle/checkstyle.xml")
    sourceSets = [sourceSets.main]

    checkstyleMain {
        source = 'src/main/java'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }

        rule {
            enabled = true
            element = 'PACKAGE'
            includes = ['com.*']

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
    dependsOn(jacocoTestReport)
}

license {
    header rootProject.file('config/copyright/copyright.header')
    excludes(["**/*.properties"])
    strictCheck true
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn licenseFormat
//check.dependsOn license
//check.dependsOn dependencyCheckAnalyze

sonarqube {
    properties {
        property "sonar.projectName", "A JPA Query By Specification framework."
        property "sonar.projectKey", "wenhao_jpa-spec"
    }
}

def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }

    scm {
        url 'https://github.com/wenhao/jpa-spec'
        connection 'https://github.com/wenhao/jpa-spec.git'
        developerConnection 'git@github.com:wenhao/jpa-spec.git'
    }

    developers {
        developer {
            id 'wenhao'
            name 'Hao Wen'
            email 'wenhao@126.com'
            organization 'Hao Wen'
            roles {
                role 'Developer'
            }
        }
    }

}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.wenhao'
            artifactId 'jpa-spec'
            version '4.0.0-SNAPSHOT'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A JPA Query By Specification framework.')
                root.appendNode('name', 'jpa-spec')
                root.appendNode('url', 'https://github.com/wenhao/jpa-spec')
                root.appendNode('inceptionYear', '2016')
                root.children().last() + pomConfig
            }
        }
    }
}

// publish to jcenter
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['maven']
    dryRun = false
    publish = true
    override = false
    pkg {
        repo = 'maven'
        name = 'jpa-spec'
        desc = 'A JPA Query By Specification framework'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/wenhao/jpa-spec'
        issueTrackerUrl = 'https://github.com/wenhao/jpa-spec/issues'
        vcsUrl = 'https://github.com/wenhao/jpa-spec'
        labels = ['jpa', 'spring-data']
        publicDownloadNumbers = true
        githubRepo = 'wenhao/jpa-spec'
        githubReleaseNotesFile = 'README.md'
        version {
            name = '4.0.0-SNAPSHOT'
            desc = 'A JPA Query By Specification framework 4.0.0-SNAPSHOT'
            released = new Date()
            vcsTag = '4.0.0-SNAPSHOT'
            gpg {
                sign = true
                passphrase = 'passphrase'
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('sonatypeUsername')
                password = project.hasProperty('sonatypePassword')
                close = '1'
            }
        }
    }
}

copy {
    from "./config/githooks/"
    into "./.git/hooks/"
    fileMode 0755
}